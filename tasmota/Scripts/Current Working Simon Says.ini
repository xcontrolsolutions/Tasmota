>D
sel=0
up=0
dn=0
t:sto=30
to=60
LED=0
stI=0
PM10=0
rssi=0
IPadd=""
IPset=0
p:venue=""
p:grp=""
p:uID=""
;>B ;Boot Before Sensors
>BS ;Boot after Sensors Initialized
->Power1 1
->hsbcolor %0LED%,100,30
=#SCRNON1
;DONT FORGET TO CLEAN
=>sensor44 c
>S ;Every Second
=> status 8
+> status 11
=#LEDCHECK1
;Timer Ran out 
if sto==0
{
    =#SCRNOFF1
}
;Screen On
if sto!=-1
{
    =#DHOME1
}
else ;Screen Off
{
    if upd[grp]>0
    or upd[venue]>0
    {
        svars
        dt[c1l5f0s1]Venue: %venue%
        dt[c1l6f0s1]Group: %grp%
        =#SCRNON1
    }
}
;every 10 seconds
if stI==0
{
    +> status
}
else
{
    +> status %stI%
}
stI+=1
;Reset to 0
if stI==11
{
    stI=0
}
;Skip 8 and 9
if stI==8
{
    stI=10
}
;>R ;Executed on Restart
>T ;Sensor Telemetry every Read and Teleperiod
PM10=SPS30#PM10
>b ;Button State Change
if bt[1]==0
{
    =#SCRNON1
}
if bt[2]==0
{
    =#SCRNON1
}
if bt[3]==0
{
    =#SCRNON1
}
>U ;JSON messages from cmd status  MUST be requested with +>
uID=StatusMQT#MqttClient
rssi=StatusSTS#Wifi#RSSI
;IPadd=StatusNet#IPaddress
#LEDCHECK1
if wifis==1
{
    if mqtts==1
    {
        LED=120 ;Green
    }
    else
    {
        LED=60  ;Amber
    }
}
else
{
    LED=0   ;Red
}
if chg[LED]>0
{
    ->hsbcolor %0LED%,100,30
}
#DHOME1
;Edit carefully there is purposeful whitespace after dt[]
if upd[dvnm]>0
{
    dt[c1l1]%dvnm%                      
}
if lip!="(IP unset)"
and IPset!=1
{
    IPset=1
    dt[c1l8]%lip%        
}
if upd[venue]>0
{
    svars
}
if upd[grp]>0
{
    svars
}
dt[c1l2]PM10 %PM10%   
if rssi>0
and chg[rssi]>0
{
    dt[c15l8]WiFi%0rssi%%%  
}
#SCRNON1
sto=to
dt[z]
dt[c1l1f0s1]%dvnm%
dt[c1l2]PM10 %PM10%
dt[c1l4]ID: %uID%
dt[c1l5]Venue: %venue%
dt[c1l6]Group: %grp%
dt[c1l8]%lip%
dt[c15l8]WiFi%0rssi%%%  
->Power2 1

#SCRNOFF1
sto=-1 ;disable timer
->Power2 0